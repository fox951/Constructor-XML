
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыПодключения_Соответствие = ОМ_Служебный.Получить_ПараметрыПриложенияAPI(Истина);
	
	ПараметрыПодключения = ОМ_Сервер.СоответствиеВСтруктуру(ПараметрыПодключения_Соответствие);

	ПараметрыПриложения  = ПараметрыПодключения[Параметры.Приложение];
	
	Приложение			 = Параметры.Приложение;
		
	ПараметрыАвторизации = ПараметрыПодключения_Соответствие.Получить(Приложение).Получить("ПараметрыАвторизации");
	
	Если ПараметрыАвторизации = Неопределено Тогда
		
		ВызватьИсключение("Не указаны параметры авторизации в " + """" + "ОМ_Служебный.Получить_ПараметрыПриложенияAPI" + """"
		+ " для " + """" + Приложение + """");
		
	КонецЕсли;
	
	Если ПараметрыАвторизации.Получить("redirect_API") <> Неопределено Тогда
		ПараметрыАвторизации.Удалить("redirect_API");	
	КонецЕсли;
	
	СтрокаПараметров 	= ОМ_Соединения.СформироватьПараметрыЗапроса(ПараметрыАвторизации);
	
	Адрес 				 = ПараметрыПриложения.Авторизация + СтрокаПараметров; 
	
КонецПроцедуры

&НаКлиенте
Процедура АдресДокументСформирован(Элемент)
	
	ТекущийАдресБраузера = Элементы.Адрес.Документ.URL;
	
	Если Приложение = "ЯндексДиск" Тогда
		
		ПозицияТокена 		 	= СтрНайти(ТекущийАдресБраузера, "code=");
		
		Если ПозицияТокена > 0 Тогда 
			
			КодАвторизации  	= Сред(ТекущийАдресБраузера, ПозицияТокена + 5);
			
			Структура_Токен 	= Получить_ТокенЯндекса(КодАвторизации);
						
			СохранитьПараметрыПодключения(Структура_Токен);			
			
		КонецЕсли;
		
	ИначеЕсли Приложение = "ГуглДиск" Тогда
		
		// Если вдруг, здесь вернуло код 403 - нужно добавить гугл пользователя здесь:
		// https://console.cloud.google.com/apis/credentials/consent?project=update1c
		// https://infostart.ru/1c/articles/1273600/
		Если ТекущийАдресБраузера = "about:blank" Тогда
			
			ИсходныйКод				= Элементы.Адрес.Документ.body.innerHTML;
			
			ПозицияТокена 		 	= СтрНайти(ИсходныйКод, "code=");
			
			Если ПозицияТокена > 0 Тогда
				
				ПозицияТокена 			= ПозицияТокена + 5;
				
				КонецТокена 			= СтрНайти(ИсходныйКод, "&amp;",, ПозицияТокена);
				
				КодАвторизации  		= Сред(ИсходныйКод, ПозицияТокена, КонецТокена - ПозицияТокена);
								
				Структура_Токен 		= Получить_ТокенГугла(КодАвторизации);
				
				СохранитьПараметрыПодключения(Структура_Токен);
				
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли Приложение = "ДропБокс" Тогда
		
		ПозицияТокена 		 	= СтрНайти(ТекущийАдресБраузера, "code=");
		
		Если ПозицияТокена > 0 Тогда 
			
			КодАвторизации  	= Сред(ТекущийАдресБраузера, ПозицияТокена + 5);
			
			Структура_Токен 	= Получить_ТокенDropBox(КодАвторизации);
			
			СохранитьПараметрыПодключения(Структура_Токен);			
			
		КонецЕсли;
		
	Иначе		
		
		ВызватьИсключение("Для приложения" + """" + Приложение + """" + " не прописан порядок авторизации.");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция Получить_ТокенDropBox(Параметр_КодАвторизации)
	
	Возврат ОМ_ОбменВебСлужебный.Получить_ТокенDropBox(Параметр_КодАвторизации, ПараметрыПриложения);
	
КонецФункции

&НаСервере
Функция Получить_ТокенГугла(Параметр_КодАвторизации)
	
	Возврат ОМ_ОбменВебСлужебный.Получить_ТокенГугла(Параметр_КодАвторизации, ПараметрыПриложения);
	
КонецФункции

&НаСервере
Функция Получить_ТокенЯндекса(Параметр_КодАвторизации)
	
	Возврат ОМ_ОбменВебСлужебный.Получить_ТокенЯндекса(Параметр_КодАвторизации, ПараметрыПриложения);
	
КонецФункции

&НаКлиенте
Процедура СохранитьПараметрыПодключения(Структура_Токен)
	
	Если Структура_Токен <> Неопределено Тогда
		
		Структура = Новый Структура;
		
		ПараметрыПодключения = Новый Структура;
		
		ПараметрыПодключения.Вставить("КодАвторизации",  		КодАвторизации);
		
		ПараметрыПодключения.Вставить("Токен", 		 	 		Структура_Токен.Токен);
		
		Если Структура_Токен.Свойство("Токен_ИД") Тогда
			ПараметрыПодключения.Вставить("Токен_ИД", 	 		Структура_Токен.Токен_ИД);	
		КонецЕсли;
		
		Если Структура_Токен.Свойство("Токен_Рефреш") Тогда
			ПараметрыПодключения.Вставить("Токен_Рефреш", 	 	Структура_Токен.Токен_Рефреш);	
		КонецЕсли;
		
		ПараметрыПодключения.Вставить("СрокДействия", 	 		Структура_Токен.СрокДействия);
		
		Структура.Вставить("ПараметрыПриложения", 				ПараметрыПриложения);
		Структура.Вставить("Приложение", 						Приложение);
		Структура.Вставить("ПараметрыПодключения",  			ПараметрыПодключения);
		
		Закрыть(Структура);
		
	Иначе
		ВызватьИсключение("Не удалось получить структуру токенов");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресПриИзменении(Элемент)
	// Вставить содержимое обработчика.
КонецПроцедуры


