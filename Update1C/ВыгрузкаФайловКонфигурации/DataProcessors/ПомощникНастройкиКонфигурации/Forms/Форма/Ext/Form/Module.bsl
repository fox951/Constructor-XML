
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонстантыНабор.Обновления1С_Логин 			= Константы.Обновления1С_Логин.Получить();
	КонстантыНабор.Обновления1С_Пароль 			= Константы.Обновления1С_Пароль.Получить();
	
	Элементы.Страницы.ТекущаяСтраница   		= Элементы.Страница_Шаг0;
	Элементы.Команда_Далее.Доступность  		= Истина;
	
	ИнфоОБазе									= ОМ_Служебный.Получить_СтрокаСоединенияБазы();
	
	ЭтоСервернаяБаза							= ИнфоОБазе.Режим = "Серверный";
	
	Элементы.ОписаниеСервернойБазы.Доступность 	= ЭтоСервернаяБаза;
	Элементы.ОписаниеФайловойБазы.Доступность 	= Не ЭтоСервернаяБаза;
	
	ВсеНастроено								= Константы.Конфигурация_НастройкаОткрывалась.Получить();
	
КонецПроцедуры

&НаКлиенте
Процедура Команда_Далее(Команда)
	
	ОчиститьСообщения();
	
	ТекущаяСтраница 					= Элементы.Страницы.ТекущаяСтраница;
	
	Результат = Истина;
	
	Если ТекущаяСтраница = Элементы.Страница_Шаг1 Тогда
			
		Если Не ЗначениеЗаполнено(КонстантыНабор.Обновления1С_Логин) Тогда
			
			ОМ_Клиент.СообщитьПользователю("Поле обязательно для заполнения.",,, "КонстантыНабор.Обновления1С_Логин");
			Результат 					= Ложь;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(КонстантыНабор.Обновления1С_Пароль) Тогда
			
			ОМ_Клиент.СообщитьПользователю("Поле обязательно для заполнения.",,, "КонстантыНабор.Обновления1С_Пароль");
			Результат 					= Ложь;
			
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.Страница_Шаг5 Тогда
		
		Если Не ЗначениеЗаполнено(КонстантыНабор.Обновления1С_КаталогХранения) Тогда
			
			ОМ_Клиент.СообщитьПользователю("Поле обязательно для заполнения.",,, "КонстантыНабор.Обновления1С_КаталогХранения");
			Результат 					= Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	СменитьСтраницу(Команда.Имя, ТекущаяСтраница, Результат);
	
КонецПроцедуры

Функция ОбработкаНаСервереПередСменойСтраницы(НомерТекСтраницы)
	
	Результат = Истина;
	
	Если НомерТекСтраницы = 1 Тогда
		
		Константы.Обновления1С_Логин.Установить(КонстантыНабор.Обновления1С_Логин);	
		Константы.Обновления1С_Пароль.Установить(КонстантыНабор.Обновления1С_Пароль);
		
		УдалосьПодключиться = ОМ_Обновления1С.ПодключитьсяК1СПортал();
		
		Если Не УдалосьПодключиться Тогда
			
			ОМ_Сервер.СообщитьПользователю("Не удалось подключиться по указанным учетным данным к порталу 1С.");
			Результат = Ложь;
			
		КонецЕсли;
		
		
	ИначеЕсли НомерТекСтраницы = 2 Тогда
		
		Константы.Обновления1С_ТолькоОтслеживаемые.Установить(Истина);
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГруппыКонфигураций.Номер КАК Номер,
		|	ГруппыКонфигураций.Ссылка КАК ГруппаСсылка
		|ИЗ
		|	Справочник.ГруппыКонфигураций КАК ГруппыКонфигураций
		|ГДЕ
		|	НЕ ГруппыКонфигураций.ПометкаУдаления
		|	И ГруппыКонфигураций.Отслеживать";
				
		ТЗПоГруппам = Запрос.Выполнить().Выгрузить();
		
		Если ТЗПоГруппам.Количество() = 0 Тогда
			ОМ_Сервер.СообщитьПользователю("Вы не выбрали отслеживаемые группы.");
			Результат = Ложь;	
		КонецЕсли;
		
	ИначеЕсли НомерТекСтраницы = 3 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Конфигурации.Ссылка КАК ГруппаСсылка
		|ИЗ
		|	Справочник.Конфигурации КАК Конфигурации
		|ГДЕ
		|	НЕ Конфигурации.ПометкаУдаления
		|	И Конфигурации.Отслеживать";
		
		ТЗПоКонфигурациям = Запрос.Выполнить().Выгрузить();
		
		Если ТЗПоКонфигурациям.Количество() = 0 Тогда
			ОМ_Сервер.СообщитьПользователю("Вы не выбрали отслеживаемые конфигурации, либо их не удалось загрузить.");
			Результат = Ложь;	
		Иначе
			
			ОбъектыЗагрузки									= ОМ_Сервер.ПрочитатьХранилище("Обновления1С_ОбъектыДляЗагрузки");
			
			Если ОбъектыЗагрузки = Неопределено Тогда
				
				ОбъектыЗагрузки 								= ОМ_Обновления1С.ОбъектыДляЗагрузкиПоУмолчанию();
				
				Для Каждого Стр Из ОбъектыЗагрузки Цикл
					
					Стр.Пометка = Ложь;
					
				КонецЦикла;
				
			КонецЕсли;
			
			КонстантыНабор.Обновления1С_АвтоОтборДат 		= Константы.Обновления1С_АвтоОтборДат.Получить();
					
			Элементы.КонстантыНаборОбновления1С_ЗагружатьРелизыОт.Доступность = Не КонстантыНабор.Обновления1С_АвтоОтборДат;
			
			ОМ_Обновления1С.ОбновитьОтборПоДате();
			
			Если КонстантыНабор.Обновления1С_АвтоОтборДат Тогда
				
				КонстантыНабор.Обновления1С_ЗагружатьРелизыОт 	= Константы.Обновления1С_ЗагружатьРелизыОт.Получить();
				
			КонецЕсли;
						
		КонецЕсли;
			
	ИначеЕсли НомерТекСтраницы = 4 Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Релизы.Ссылка КАК ГруппаСсылка
		|ИЗ
		|	Справочник.Релизы КАК Релизы
		|ГДЕ
		|	НЕ Релизы.ПометкаУдаления
		|	И Релизы.Владелец.Отслеживать";
		
		ТЗПоРелизам = Запрос.Выполнить().Выгрузить();
		
		Если ТЗПоРелизам.Количество() = 0 Тогда
			
			ОМ_Сервер.СообщитьПользователю("Вы не выбрали отслеживаемые конфигурации, либо релизы не удалось загрузить.");
			Результат = Ложь;
			
		Иначе
			
			ОбъектыЕсть = ОМ_Обновления1С.ОбъектыДляЗагрузкиПоУмолчанию(Истина).Количество() > 0;
			
			Если Не ОбъектыЕсть Тогда
				ОМ_Сервер.СообщитьПользователю("Вы не выбрали объекты для загрузки. Необходимо установить флажок здесь, напротив нужного объекта для скачивания.",,, "ОбъектыЗагрузки");	
				Результат = Ложь;	
			КонецЕсли;
			
		КонецЕсли;
		
		КонстантыНабор.Обновления1С_КаталогХранения = Константы.Обновления1С_КаталогХранения.Получить();
		
	ИначеЕсли НомерТекСтраницы = 5 Тогда
		
		ДопустимоеЧислоПотоков = Константы.ФоновыеЗадания_КоличествоПотоков.Получить();
		
		Если ДопустимоеЧислоПотоков = 0 
			Или КонстантыНабор.ФоновыеЗадания_КоличествоПотоков = 0 Тогда
			
			ДопустимоеЧислоПотоков 							= ОМ_РегламентныеОперации.МаксЧислоПотоковПоУмолчанию();
			
			КонстантыНабор.ФоновыеЗадания_КоличествоПотоков = ДопустимоеЧислоПотоков;
			
		Иначе
			
			ДопустимоеЧислоПотоков							= КонстантыНабор.ФоновыеЗадания_КоличествоПотоков;	
			
		КонецЕсли;
		
		Константы.ФоновыеЗадания_КоличествоПотоков.Установить(ДопустимоеЧислоПотоков);
		
		//////////////////////////
		
		КонстантыНабор.Обновления1С_АвтоЗагрузка			= Константы.Обновления1С_АвтоЗагрузка.Получить();
		
		Элементы.Группа_НастройкаРасписания.Доступность     = КонстантыНабор.Обновления1С_АвтоЗагрузка;
		
		Расписание = ОМ_Сервер.ПрочитатьХранилище("Обновления1С_Расписание");
		
		Если Расписание = Неопределено 
			Или Не КонстантыНабор.Обновления1С_АвтоЗагрузка Тогда
			
			Расписание = Новый РасписаниеРегламентногоЗадания;
			
		КонецЕсли;
		
		Если Расписание = Новый РасписаниеРегламентногоЗадания Тогда
			Элементы.ПредставлениеРасписания.Заголовок = "<Не установлено>";
		Иначе
			Элементы.ПредставлениеРасписания.Заголовок = Строка(Расписание);
		КонецЕсли;
		
	ИначеЕсли НомерТекСтраницы = 6 Тогда
		
		Константы.Конфигурация_НастройкаОткрывалась.Установить(Истина);
		
		Константы.Журнал_ВестиЛог.Установить(Истина);
		Константы.Журнал_ВыводитьСостоние.Установить(Истина);
		
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеЗаголовкаСтраницы(НомерШага)
	
	Соответствие = Новый Соответствие;
	
	Соответствие.Вставить(0, "Введение");
	Соответствие.Вставить(1, "Шаг 1: Подключение к ИТС");
	Соответствие.Вставить(2, "Шаг 2: Загрузка и выбор разделов");
	Соответствие.Вставить(3, "Шаг 3: Загрузка и выбор конфигураций");
	Соответствие.Вставить(4, "Шаг 4: Выбор загружаемых объектов и загрузка релизов");
	Соответствие.Вставить(5, "Шаг 5: Настройка каталога выгрузки");
	Соответствие.Вставить(6, "Шаг 6: Дополнительные настройки");
	Соответствие.Вставить(7, "Шаг 7: Первая загрузка обновлений 1С");
	
	Возврат Соответствие.Получить(НомерШага);
	
КонецФункции

&НаКлиенте
Функция ПолучитьСоответствиеСтраницПоШагам(Обратное = Ложь)
	
	Соответствие = Новый Соответствие;
		
	Для Каждого Элемент Из Элементы.Страницы.ПодчиненныеЭлементы Цикл
		
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы")
			И Элемент.Вид = ВидГруппыФормы.Страница
			И СтрНачинаетсяС(Элемент.Имя, "Страница_Шаг") Тогда
			
			НомерШага = Число(СокрЛП(СтрЗаменить(Элемент.Имя, "Страница_Шаг", "")));
			
			Если Не Обратное Тогда
				Соответствие.Вставить(Элемент, НомерШага);
			Иначе
				Соответствие.Вставить(НомерШага, Элемент);
			КонецЕсли;
						
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Соответствие;
	
КонецФункции

&НаКлиенте
Процедура СменитьСтраницу(ИмяКоманды, ТекущаяСтраница, Результат = Ложь)
	
	Если Не Результат Тогда
		Возврат;
	КонецЕсли;
		
	СоответствиеСтраницПоШагам_СтраницаНомер 	= ПолучитьСоответствиеСтраницПоШагам();
	СоответствиеСтраницПоШагам_НомерСтраница 	= ПолучитьСоответствиеСтраницПоШагам(Истина);
	
	НомерТекСтраницы = СоответствиеСтраницПоШагам_СтраницаНомер.Получить(ТекущаяСтраница);
	
	Если ИмяКоманды = "Команда_Далее" Тогда
		ПродолжатьВыполнение = ОбработкаНаСервереПередСменойСтраницы(НомерТекСтраницы);
	Иначе
		ПродолжатьВыполнение = Истина;
	КонецЕсли;

	Если Не ПродолжатьВыполнение Тогда
		Возврат;
	КонецЕсли;
	
	Если НомерТекСтраницы = Неопределено Тогда		
		ВызватьИсключение("Не найден номер шага для страницы " + """" + ТекущаяСтраница.Имя + """");	
	КонецЕсли;
	
	Если ИмяКоманды = "Команда_Далее" Тогда
		НомерСледующей = НомерТекСтраницы + 1;	
	Иначе
		НомерСледующей = НомерТекСтраницы -1;
	КонецЕсли;
	
	СледующаяСтраница = СоответствиеСтраницПоШагам_НомерСтраница.Получить(НомерСледующей); 	
	
	Если СледующаяСтраница <> Неопределено Тогда
		
		Элементы.Страницы.ТекущаяСтраница 	= СледующаяСтраница;
		Элементы.ТекущийШаг.Заголовок 		= ПолучитьСоответствиеЗаголовкаСтраницы(НомерСледующей);
		
	КонецЕсли;
	
	/////////////////////////////////////////
	
	Если ИмяКоманды = "Команда_Далее" Тогда
		НомерСледующей2 = НомерСледующей + 1;	
	Иначе
		НомерСледующей2 = НомерСледующей -1;
	КонецЕсли;

	СледующаяСтраница2 = СоответствиеСтраницПоШагам_НомерСтраница.Получить(НомерСледующей2);
	
	Элементы.Команда_Далее.Доступность 		= СледующаяСтраница2 <> Неопределено;
	Элементы.Команда_Назад.Доступность		= НомерСледующей2 > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура Команда_Назад(Команда)
	
	ОчиститьСообщения();
	
	ТекущаяСтраница = Элементы.Страницы.ТекущаяСтраница;
	СменитьСтраницу(Команда.Имя, ТекущаяСтраница, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Общая_ПереходВСправочник(Элемент)
	
	ИмяФормыДляОткрытия = "";
	
	Если Элемент.Имя = "ГиперСсылка_ДеревоРазделов" Тогда
		
		ИмяФормыДляОткрытия = "Справочник.ГруппыКонфигураций.Форма.ФормаСписка";
		
	ИначеЕсли Элемент.Имя = "ГиперСсылка_Конфигурации" Тогда 
		
		ИмяФормыДляОткрытия = "Справочник.Конфигурации.Форма.ФормаСписка";
		
	ИначеЕсли Элемент.Имя = "ГиперСсылка_КаталогЗагрузки" Тогда
		
		ИмяФормыДляОткрытия = "Справочник.КаталогиЗагрузки.Форма.ФормаСписка";
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ИмяФормыДляОткрытия) Тогда
		
		ФормаРазделов = ПолучитьФорму(ИмяФормыДляОткрытия, Новый Структура("ЭтоПомощник"), ЭтаФорма);
		ФормаРазделов.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКонфигурации(Команда)
	
	ЗагрузитьКонфигурацииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьДеревоРазделов(Команда)
	
	ЗагрузитьДеревоРазделовНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьКонфигурацииНаСервере()
	
	ОМ_РегламентныеОперации.ЗагрузкаКонфигураций(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДеревоРазделовНаСервере()
	
	ОМ_РегламентныеОперации.ОбновитьДеревоРазделов();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонстантНаСервере()
	
	Элементы.КонстантыНаборОбновления1С_ЗагружатьРелизыОт.Доступность = Не КонстантыНабор.Обновления1С_АвтоОтборДат;
	
	Константы.Обновления1С_АвтоОтборДат.Установить(КонстантыНабор.Обновления1С_АвтоОтборДат);
	
	ОМ_Обновления1С.ОбновитьОтборПоДате();
	
	Если КонстантыНабор.Обновления1С_АвтоОтборДат Тогда
			
		КонстантыНабор.Обновления1С_ЗагружатьРелизыОт 	= Константы.Обновления1С_ЗагружатьРелизыОт.Получить();
		
	Иначе
		
		Константы.Обновления1С_ЗагружатьРелизыОт.Установить(КонстантыНабор.Обновления1С_ЗагружатьРелизыОт);
		
	КонецЕсли;
	
	ОМ_Обновления1С.Установить_ОбъектыЗагрузки(ОбъектыЗагрузки);
	
	Константы.Обновления1С_КаталогХранения.Установить(КонстантыНабор.Обновления1С_КаталогХранения);
	
	ДопустимоеЧислоПотоков 								= Константы.ФоновыеЗадания_КоличествоПотоков.Получить();
	
	Если ДопустимоеЧислоПотоков = 0 
		Или КонстантыНабор.ФоновыеЗадания_КоличествоПотоков = 0 Тогда
		
		ДопустимоеЧислоПотоков 							= ОМ_РегламентныеОперации.МаксЧислоПотоковПоУмолчанию();
		
		КонстантыНабор.ФоновыеЗадания_КоличествоПотоков = ДопустимоеЧислоПотоков;
		
	Иначе
		
		ДопустимоеЧислоПотоков							= КонстантыНабор.ФоновыеЗадания_КоличествоПотоков;	
		
	КонецЕсли;
	
	Константы.ФоновыеЗадания_КоличествоПотоков.Установить(ДопустимоеЧислоПотоков);
	
	Константы.Обновления1С_АвтоЗагрузка.Установить(КонстантыНабор.Обновления1С_АвтоЗагрузка);
	Константы.Обновления1С_Расписание.Установить(ОМ_Сервер.СложитьВХранилище(Расписание));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКонстантНаКлиенте(Элемент)
	
	ПриИзмененииКонстантНаСервере();
	
	Оповестить("ПеречитатьКонстанты");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРелизы(Команда)
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(КонстантыНабор.Обновления1С_ЗагружатьРелизыОт) Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРелизыЗавершение", ЭтотОбъект), "Вы не заполнили дату отбора по релизам. В таком случае будут загружены ВСЕ релизы с портала 1С с начала времен. Продолжить?", РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьРелизыНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРелизыЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ЗаполнитьРелизыНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРелизыНаСервере()
	
	ЕстьЧтоКачать = ОМ_Обновления1С.ОбъектыДляЗагрузкиПоУмолчанию(Истина).Количество() > 0;
	
	Если ЕстьЧтоКачать Тогда
		ОМ_РегламентныеОперации.ЗагрузкаРелизов();
	Иначе
		ОМ_Сервер.СообщитьПользователю("Вы не выбрали объекты для загрузки. Необходимо установить флажок здесь, напротив нужного объекта для скачивания.",,, "ОбъектыЗагрузки");	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеНажатие(Элемент)
	
	Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	Диалог.Показать(Новый ОписаниеОповещения("ОткрытьРасписаниеЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьРасписаниеЗавершение(НовоеРасписание, Контекст) Экспорт

	Если НовоеРасписание <> Неопределено Тогда
		
		Расписание = НовоеРасписание;

	Иначе
		Возврат;
	КонецЕсли;
	
	Если Расписание = Новый РасписаниеРегламентногоЗадания Тогда
		Элементы.ПредставлениеРасписания.Заголовок = "<Не установлено>";
	Иначе
		Элементы.ПредставлениеРасписания.Заголовок = Строка(Расписание);
	КонецЕсли;
	
	ПриИзмененииКонстантНаСервере();
	
	Оповестить("ПеречитатьКонстанты");
	
КонецПроцедуры

&НаКлиенте
Процедура КонстантыНаборОбновления1С_АвтоЗагрузкаПриИзменении(Элемент)
	
	Элементы.Группа_НастройкаРасписания.Доступность = КонстантыНабор.Обновления1С_АвтоЗагрузка;
	
	ПриИзмененииКонстантНаСервере();
	
	Оповестить("ПеречитатьКонстанты");
	
КонецПроцедуры

&НаКлиенте
Процедура Перезапустить(Команда)
	
	ЭтоПерезапуск = Истина;
	ЗавершитьРаботуСистемы(Истина, Истина); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если Не ЗавершениеРаботы 
		И Не ЭтоПерезапуск 
		И Не ВсеНастроено Тогда
		
		ЗавершитьРаботуСистемы(Ложь, Ложь);
		
	КонецЕсли;
	
КонецПроцедуры


